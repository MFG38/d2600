actor Sentry 21000
{
	//$category D2600_Enemies
	spawnid 5
	health 40
	painchance 128
	speed 6
	height 25	//Slightly over the actual sprite height of 24px so that the player can't walk on(to) them.
	radius 12
	mass 75
	maxstepheight 1
	obituary "A mere sentry eliminated %o."
	activesound "sentry/idle"
	seesound "sentry/see"
	painsound "general/pain"
	deathsound "sentry/death"
	MONSTER
	+NOBLOOD
	+SEEINVISIBLE
	States
	{
	Spawn:
		SNTR ABCBADED 4 A_Wander
		SNTR A 0 A_Look
		Loop
	See:
		SNTR ABCBADED 4 A_Chase
		Loop
	Missile:
		SNTR F 4 A_FaceTarget
		SNTR F 1 A_CustomMissile("SentryShot",14)
		SNTR F 1 A_CustomMissile("SentryShot",14)
		SNTR F 4 A_CustomMissile("SentryShot",14)
		Goto See
	Pain:
		SNTR G 2 BRIGHT A_Pain
		SNTR A 2
		SNTR G 2 BRIGHT
		Goto See
	Death:
		SNTR G 4 A_Scream
		SNTR GGGGGGGGGG 4 A_FadeOut(0.1)
		Stop
	}
}

actor SentryShot
{
	height 4
	radius 2
	damage (2 + random(0,3))
	speed 24
	renderstyle translucent
	alpha 0.9
	seesound "general/attack"
	PROJECTILE
	States
	{
	Spawn:
		SPRJ A 1 BRIGHT
		Loop
	Death:
		SPRJ A 2
		Stop
	}
}
