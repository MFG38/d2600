actor Beetle 21004
{
	//$category D2600_Enemies
	spawnid 10
	health 300
	painchance 80
	speed 6
	height 32
	radius 16
	mass 9999
	obituary "A beetle was faster than %o."
	maxstepheight 1
	seesound "beetle/see"
	activesound "beetle/idle"
	painsound "general/pain"
	deathsound "beetle/death"
	MONSTER
	+NOBLOOD
	States
	{
	Spawn:
		BETL A 0 A_SetInvulnerable
		BETL ABCDE 4 A_Wander
		BETL A 0 A_Look
		Loop
	See:
		BETL ABCDE 4 A_Chase
		Loop
	Missile:
		BETL F 3 A_FaceTarget
		BETL F 3 A_UnsetInvulnerable
		BETL GHI 3
		BETL I 4 A_CustomMissile("BeetleShot",24)
		BETL IHG 3
		BETL F 3 A_SetInvulnerable
		Goto See
	Pain:
		BETL J 2 BRIGHT A_Pain
		BETL A 2
		BETL J 2 BRIGHT
		Goto See
	Death:
		BETL J 4 A_Scream
		BETL JJJJJJJJJJ 4 A_FadeOut(0.1)
		Stop	
	}
}

actor BeetleShot
{
	height 8
	radius 4
	speed 12
	damage 8
	renderstyle translucent
	alpha 0.9
	seesound "beetle/attack"
	PROJECTILE
	States
	{
	Spawn:
		BPRJ A 1 BRIGHT
		Loop
	Death:
		BPRJ A 2
		Stop
	}	
}
