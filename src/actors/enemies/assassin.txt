actor Assassin 21001
{
	//$category D2600_Enemies
	spawnid 7
	health 200
	painchance 160
	speed 25
	height 48
	radius 12
	mass 100
	obituary "An assassin hunted %o down."
	maxstepheight 1
	painsound "general/pain"
	seesound "assassin/see"
	activesound "assassin/idle"
	deathsound "assassin/death"
	MONSTER
	+NOBLOOD
	States
	{
	Spawn:
		ASSN ABCBADED 2 A_Wander
		ASSN A 0 A_Look
		Loop
	See:
		ASSN ABCBADED 2 A_Chase
		Loop
	Melee:
		ASSN A 0 A_Jump(128,"Melee1")
		ASSN A 0 A_Jump(128,"Melee2")
		ASSN A 0 A_Jump(96,"DoubleMelee")
	Melee1:
		ASSN F 0 A_FaceTarget
		ASSN F 6 A_PlaySound("assassin/attack")
		ASSN G 6 A_CustomMeleeAttack(8)
		Goto See
	Melee2:
		ASSN H 0 A_FaceTarget
		ASSN H 6 A_PlaySound("assassin/attack")
		ASSN I 6 A_CustomMeleeAttack(8)
		Goto See
	DoubleMelee:
		ASSN J 0 A_FaceTarget
		ASSN J 6 A_PlaySound("assassin/attack")
		ASSN K 12 A_CustomMeleeAttack(16)
		Goto See
	Pain:
		ASSN L 2 BRIGHT A_Pain
		ASSN A 2
		ASSN L 2 BRIGHT
		Goto See
	Death:
		ASSN L 4 A_Scream
		ASSN LLLLLLLLLL 4 A_FadeOut(0.1)
		Stop
	}
}
