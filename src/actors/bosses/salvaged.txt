actor SalvagedOne 21203
{
	//$category D2600_Bosses
	spawnid 153
	health 12500
	painchance 32
	speed 8
	height 72
	radius 30
	mass 9999
	obituary "The Salvaged One ended %o's suffering."
	maxstepheight 0
	painsound "general/pain"
	seesound "salvaged/see"
	activesound "salvaged/active"
	deathsound "salvaged/death"
	MONSTER
	+BOSS
	+SEEINVISIBLE
	+MISSILEMORE
	+MISSILEEVENMORE
	+NOBLOOD
	States
	{
	Spawn:
		FBOS A 2 A_Look
		Loop
	See:
		FBOS ABCBADED 3 A_Chase
		Loop
	Missile:
		FBOS A 0 A_Jump(128,"Missile1")
		FBOS A 0 A_Jump(128,"Missile2")
		FBOS A 0 A_Jump(80,"ActualMissile")
	Missile1:
		FBOS A 10
		FBOS A 0 A_CustomMissile("SalvagedOnePlasmaBall",46,24)
		FBOS A 4 A_CustomMissile("SalvagedOnePlasmaBall",46,-24)
		FBOS A 0 A_CustomMissile("SalvagedOnePlasmaBall",46,24)
		FBOS A 4 A_CustomMissile("SalvagedOnePlasmaBall",46,-24)
		FBOS A 0 A_CustomMissile("SalvagedOnePlasmaBall",46,24)
		FBOS A 4 A_CustomMissile("SalvagedOnePlasmaBall",46,-24)
		FBOS A 0 A_CustomMissile("SalvagedOnePlasmaBall",46,24)
		FBOS A 4 A_CustomMissile("SalvagedOnePlasmaBall",46,-24)
		FBOS A 0 A_CustomMissile("SalvagedOnePlasmaBall",46,24)
		FBOS A 4 A_CustomMissile("SalvagedOnePlasmaBall",46,-24)
		FBOS A 6
		Goto See
	Missile2:
		FBOS A 10
		FBOS A 0 A_CustomMissile("SalvagedOneRipperDisc",40,24)
		FBOS A 2 A_CustomMissile("SalvagedOneRipperDisc",40,-24)
		FBOS A 8
		Goto See
	ActualMissile:
		FBOS A 10
		FBOS A 5 A_CustomMissile("MicroMissile2",68)
		Goto Missile
	Pain:
		FBOS F 2 BRIGHT A_Pain
		FBOS A 2
		FBOS F 2 BRIGHT
		Goto See
	Death:
		FBOS F 0 A_Scream
		FBOS FFFFFFFFFF 4 A_FadeOut(0.1)
		Stop
	}
}

actor SalvagedOnePlasmaBall
{
	height 12
	radius 6
	damage 5
	speed 24
	renderstyle translucent
	alpha 0.8
	seesound "general/attack"
	PROJECTILE
	+STRIFEDAMAGE
	States
	{
	Spawn:
		FBP1 A 1 BRIGHT
		Loop
	Death:
		FBP1 A 2
		Stop
	}
}

actor SalvagedOneRipperDisc
{
	height 5
	radius 7
	damage 10
	speed 40
	renderstyle translucent
	alpha 0.8
	seesound "general/ripperdisc"
	PROJECTILE
	States
	{
	Spawn:
		FBP1 A 1 BRIGHT A_SpawnItem("RipperDiscTrail")
		Loop
	Death:
		FBP1 A 2
		Stop
	}
}

actor RipperDiscTrail
{
	renderstyle translucent
	alpha 0.8
	+NOGRAVITY
	States
	{
	Spawn:
		FBP2 AAAAAAAAAA 1 BRIGHT A_FadeOut(0.1)
		Stop
	}
}
