actor GreatImmortal 21200
{
	//$category D2600_Bosses
	spawnid 150
	health 6000
	painchance 96
	speed 10
	height 96
	radius 32
	mass 9999
	obituary "The Great Immortal slaughtered %o."
	maxstepheight 0
	painsound "general/pain"
	seesound "immortal/see"
	deathsound "immortal/death"
	MONSTER
	+NOBLOOD
	+BOSS
	+MISSILEEVENMORE
	+SEEINVISIBLE
	States
	{
	Spawn:
		GRIM A 2 A_Look
		Loop
	See:
		GRIM ABCBADED 4 A_Chase
		Loop
	Missile:
		GRIM A 0 A_Jump(128,"Missile1")
		GRIM A 0 A_Jump(128,"Missile2")
	Missile1:
		GRIM F 8
		GRIM F 4 A_CustomMissile("ImmortalHoming",59)
		GRIM F 8
		Goto See
	Missile2:
		GRIM F 12
		GRIM FFFFF 1 A_CustomMissile("ImmortalLaser",59)
		GRIM F 8
		Goto See
	Pain:
		GRIM G 2 BRIGHT A_Pain
		GRIM A 2
		GRIM G 2 BRIGHT
		Goto See
	Death:
		TNT1 A 0 A_SpawnItemEx("LaserCannon",0,0,56)
		GRIM G 4 A_Scream
		GRIM GGGGGGGGGG 4 A_FadeOut(0.1)
		Stop
	}
}

actor ImmortalLaser
{
	height 8
	radius 4
	damage 3
	speed 32
	renderstyle translucent
	alpha 0.9
	seesound "general/attack"
	PROJECTILE
	States
	{
	Spawn:
		IPR1 A 1 BRIGHT
		Loop
	Death:
		IPR1 A 2
		Stop
	}
}

actor ImmortalHoming
{
	height 16
	radius 8
	damage 10
	speed 12
	renderstyle translucent
	alpha 0.9
	seesound "general/attack"
	PROJECTILE
	+SEEKERMISSILE
	States
	{
	Spawn:
		IPR2 A 1 BRIGHT A_SeekerMissile(0,4)
		Loop
	Death:
		IPR2 A 2
		Stop
	}
}
