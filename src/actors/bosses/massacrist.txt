actor Massacrist 21201
{
	//$category D2600_Bosses
	spawnid 151
	health 5600
	painchance 64
	speed 8
	height 112
	radius 40
	mass 9999
	obituary "The Massacrist massacred %o."
	maxstepheight 0
	painsound "general/pain"
	seesound "massacrist/see"
	deathsound "massacrist/death"
	MONSTER
	+NOBLOOD
	+BOSS
	+MISSILEEVENMORE
	+SEEINVISIBLE
	States
	{
	Spawn:
		MASR A 2 A_Look
		Loop
	See:
		MASR ABACAD 3 A_Chase
		Loop
	Missile:
		MASR A 0 A_Jump(128,"Missile1")
		MASR A 0 A_Jump(128,"Missile2")
		MASR A 0 A_Jump(112,"Missile3")
	Missile1:
		MASR G 12
		MASR G 4 A_CustomMissile("MassacristPlasmaBall",60,28)
		MASR G 4 A_CustomMissile("MassacristPlasmaBall",60,-28)
		MASR G 4 A_CustomMissile("MassacristPlasmaBall",60,28)
		MASR G 4 A_CustomMissile("MassacristPlasmaBall",60,-28)
		MASR G 4
		Goto See
	Missile2:
		MASR F 12
		MASR F 0 A_CustomMissile("MassacristMatrix",60,28)
		MASR F 4 A_CustomMissile("MassacristMatrix",60,-28)
		MASR F 4
		Goto See
	Missile3:
		MASR E 12
		MASR E 0 A_CustomMissile("MassacristThunderball",60,28)
		MASR E 4 A_CustomMissile("MassacristThunderball",60,-28)
		MASR E 4
		Goto See
	Pain:
		MASR H 2 BRIGHT A_Pain
		MASR A 2
		MASR H 2 BRIGHT
		Goto See
	Death:
		TNT1 A 0 A_SpawnItemEx("ThunderballCannon",0,0,56)
		MASR H 4 A_Scream
		MASR HHHHHHHHHH 4 A_FadeOut(0.1)
		Stop
	}
}

actor MassacristPlasmaBall
{
	height 16
	radius 8
	damage 8
	speed 32
	renderstyle translucent
	alpha 0.8
	seesound "general/attack"
	PROJECTILE
	States
	{
	Spawn:
		MSP2 A 1 BRIGHT
		Loop
	Death:
		MSP2 A 2
		Stop
	}
}

actor MassacristMatrix
{
	height 20
	radius 10
	damage 12
	speed 8
	renderstyle translucent
	alpha 0.8
	seesound "general/attack"
	PROJECTILE
	States
	{
	Spawn:
		MSP1 A 0 A_SeekerMissile(0,2)
		MSP1 A 1 A_SpawnItem("MassacristMatrixTrail")
		Loop
	Death:
		MSP1 A 2
		Stop
	}
}

actor MassacristMatrixTrail
{
	renderstyle translucent
	alpha 0.8
	+NOGRAVITY
	States
	{
	Spawn:
		MSP1 ABCDEFGH 1 A_FadeOut(0.13)
		Stop
	}
}

actor MassacristThunderball
{
	height 16
	radius 8
	speed 8
	renderstyle translucent
	alpha 0.5
	seesound "general/laser"
	PROJECTILE
	States
	{
	Spawn:
		MSP3 ABC 3 BRIGHT A_Explode(5,96,XF_NOTMISSILE)
		Loop
	Death:
		MSP3 A 2
		Stop
	}
}
